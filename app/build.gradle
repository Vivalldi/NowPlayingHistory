apply plugin: 'com.android.application'

ext.versionMajor = 1
ext.versionMinor = 3
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = false
ext.minimumSdkVersion = 26

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "io.cosgrove.nowplaying_history"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 26
        versionCode generateVersionCode() // 150010203
        versionName generateVersionName() // 1.2.3-SNAPSHOT
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "NowPlayingHistory-v$versionName($versionCode)")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile 'com.android.support:recyclerview-v7:26.1.+'
    compile 'com.heinrichreimersoftware:material-intro:1.6.2'
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null) {
        if (ext.isSnapshot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
